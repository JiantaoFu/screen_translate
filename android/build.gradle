buildscript {
    ext {
        kotlin_version = '2.1.0'
        compileSdkVersion = 35
        targetSdkVersion = 35
        minSdkVersion = 23
        javaVersion = JavaVersion.VERSION_11  // Updated to Java 11
    }
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                compileSdkVersion rootProject.ext.compileSdkVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }

                compileOptions {
                    sourceCompatibility rootProject.ext.javaVersion
                    targetCompatibility rootProject.ext.javaVersion
                }
            }

            if (project.plugins.hasPlugin('kotlin-android')) {
                project.android.kotlinOptions {
                    jvmTarget = rootProject.ext.javaVersion.toString()
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
